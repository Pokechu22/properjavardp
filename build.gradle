apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

version = "1.1-tweaked-0.4-SNAPSHOT"
group = "unoficial.net.propero"
archivesBaseName = "properjavardp"

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir "resources"
        }
    }
    test {
        java {
            srcDir 'test'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    mcdep
    compile.extendsFrom(mcdep)
}

dependencies {
    mcdep files('lib/java-getopt-1.0.13.jar') // woo, old java
    compile "org.apache.logging.log4j:log4j-api:2.8.1"
    compile "org.apache.logging.log4j:log4j-core:2.8.1"

    compile "com.google.code.findbugs:jsr305:3.0.1"
    compile "org.bouncycastle:bcprov-jdk15on:1.57"
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-library:1.3"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Contains dependencies not present in MC
task mcdepJar(type: Jar) {
    classifier = 'mcdep'
    from { configurations.mcdep.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    exclude '**/log4j2.xml'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives jar
    archives mcdepJar
    archives sourcesJar
}
